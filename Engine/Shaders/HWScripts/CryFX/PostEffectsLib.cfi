// CRYENGINE Source Code File
// Copyright 2001-2015 Crytek GmbH. All rights reserved.

//////////////////////////////// Common constants ////////////////

// Common vertex and pixel shader params 
//    - use for explicitly setting values from code

float4 vsParams[16] < register = c0; >;
float4 psParams[16] < register = c50; >;

float4   RotGridScreenOffset : PB_RotGridScreenOff;
float   AnimGenParams = { PB_time 1.0 };
float4   WorldViewPos  : PB_CameraPos;

float4   CameraUp : PB_CameraUp;
float4   CameraRight : PB_CameraRight;
float4   CamFront : PB_CameraFront;

float4  ScrSize : PB_ScreenSize;

float4  g_vQuadParams;
float4  g_vQuadPosParams;
float4  g_vViewFrustumLT;
float4  g_vViewFrustumLB;
float4  g_vViewFrustumRT;
float4  g_vViewFrustumRB;

// x, y, z - unused, w - particle lifetime
float4  vScrParticleInfo;
	
#define RAIN_OCC_MAP_SIZE 256
#define WATER_RIPPLES_SIM_GRID_SIZE 25

//////////////////////////////// Common samplers ////////////////

// Use when explicitly binding from code (_tex to void name colision)
sampler2D _tex0 : register(s0);
sampler2D _tex1 : register(s1);
sampler2D _tex2 : register(s2);
sampler2D _tex3 : register(s3);
sampler2D _tex4 : register(s4);
sampler2D _tex5 : register(s5);
sampler2D _tex6 : register(s6);
sampler2D _tex7 : register(s7);
sampler2D _tex8 : register(s8);
sampler2D _tex9 : register(s9);
sampler2D _texA : register(s10);
sampler2D _texB : register(s11);
sampler2D _texC : register(s12);
sampler2D _texD : register(s13);
sampler2D _texE : register(s14);
sampler2D _texF : register(s15);

Texture2D<float4> _tex3_D3D11 : register(t3);

samplerCUBE _cubetex0 : register(s0);
samplerCUBE _cubetex1 : register(s1);
samplerCUBE _cubetex2 : register(s2);
samplerCUBE _cubetex3 : register(s3);
samplerCUBE _cubetex4 : register(s4);
samplerCUBE _cubetex5 : register(s5);
samplerCUBE _cubetex6 : register(s6);
samplerCUBE _cubetex7 : register(s7);

// Use for auto binding
sampler2D screenMapSampler : register (s0)
{
  Texture = $BackBuffer;
  MinFilter = POINT;
  MagFilter = POINT;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

sampler2D depthMapSampler  : register (s1)
{
  Texture = $ZTarget;
  MinFilter = POINT;
  MagFilter = POINT;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

sampler2D screenMapScaledSampler_d2  : register (s2)
{
  Texture = $BackBufferScaled_d2;
  MinFilter = LINEAR;
  MagFilter = LINEAR;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

sampler2D screenMapScaledSampler_d4  : register (s3)
{
  Texture = $BackBufferScaled_d4;
  MinFilter = LINEAR;
  MagFilter = LINEAR;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

sampler2D screenMapScaledSampler_d8  : register (s4)
{
  Texture = $BackBufferScaled_d8;
  MinFilter = LINEAR;
  MagFilter = LINEAR;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

sampler2D screenTargetSampler : register (s0)
{
  Texture = $SceneTarget;
  MinFilter = POINT;
  MagFilter = POINT;
  MipFilter = POINT; 
  AddressU = Clamp;
  AddressV = Clamp;	
};

//////////////////////////////// Common vertex shader stuff ////////////////

struct pixout_MRT
{
  float4 Color  : COLOR0;
  float4 Color1 : COLOR1;
};

struct vtxIn
{
  IN_P
  IN_TBASE
	IN_C0
};

struct vtxInWPOS
{
  IN_P
  IN_TBASE
  float3 CamVec : TEXCOORD1;
};

struct vtxOut
{
  OUT_HPOS_IN_WPOS
  float4 baseTC     : TEXCOORD0;
};

struct vtxOutWPOS
{
  OUT_HPOS_IN_WPOS
  float4 baseTC      : TEXCOORD0;
  float4 CamVec      : TEXCOORD1;

  MSAA_SAMPLE_INDEX_PS
};

float4 GetHPositionFsQuad(float4 vPos)
{
	vPos.xy = (vPos.xy)? g_vQuadPosParams.zw : g_vQuadPosParams.xy;
	vPos.z = g_vQuadParams.z;
	return  Get2dHPos(vPos);
}

float3 GetCamVecFsQuad(float4 vPos)
{
	float3 vCamVecLerpT = (vPos.x)? g_vViewFrustumRT : g_vViewFrustumLT;
	float3 vCamVecLerpB = (vPos.x)? g_vViewFrustumRB : g_vViewFrustumLB;	
	return (vPos.y)? vCamVecLerpB : vCamVecLerpT;
}

///////////////// vertex shader //////////////////

float4 GetHPos_FromTriVertexID(uint vertexID)
{
	return float4(float2(((vertexID << 1) & 2) * 2.0, (vertexID == 0) ? -4.0 : 0.0) + float2(-1.0, 1.0), 0.0, 1.0);
}

float2 GetBaseTC_FromTriVertexID(uint vertexID)
{
	return float2((vertexID << 1) & 2, (vertexID == 0) ? 2.0 : 0.0);
}

vtxOut FullscreenTriVS(uint VertexID : SV_VertexID)
{
	vtxOut OUT = (vtxOut)0;
	OUT.HPosition = GetHPos_FromTriVertexID(VertexID);
	OUT.baseTC.xy = GetBaseTC_FromTriVertexID(VertexID);
	return OUT;
}

// deprecated
vtxOut FsQuadVS(vtxIn IN)
{
	vtxOut OUT = (vtxOut) 0; 
	OUT.HPosition = GetHPositionFsQuad(IN.Position);
	OUT.baseTC.xy = IN.baseTC.xy;
	return OUT;
}

// deprecated
vtxOutWPOS FsQuadCamVecVS(vtxIn IN)
{
	vtxOutWPOS OUT = (vtxOutWPOS) 0;
	OUT.HPosition = GetHPositionFsQuad(IN.Position);
	OUT.CamVec.xyz = GetCamVecFsQuad(IN.Position);
	OUT.baseTC.xy = IN.baseTC.xy;
	return OUT;
}

// deprecated, can be removed once all dependent techniques updated to use DrawQuadFS from cpp
vtxOut BaseVS(vtxIn IN)
{
  vtxOut OUT = (vtxOut)0; 
	OUT.HPosition = Get2dHPos(IN.Position);  
  OUT.baseTC.xy = IN.baseTC.xy;

  // output with correct aspect ratio into wz
  OUT.baseTC.wz = (IN.baseTC.xy -0.5 ) * float2(0.75*(ScrSize.x/ScrSize.y), 1.0) + 0.5;
  return OUT;
}

// deprecated
vtxOut CustomViewPortBaseVS(vtxIn IN)
{
  vtxOut OUT = (vtxOut)0; 

  float4 vPos = IN.Position;
  OUT.HPosition = mul(vpMatrix, vPos);  
  OUT.baseTC.xy = IN.baseTC.xy;

  // output with correct aspect ratio into wz
  OUT.baseTC.wz = (IN.baseTC.xy -0.5 ) * float2(0.75*(ScrSize.x/ScrSize.y), 1.0) + 0.5;

  return OUT;
}

// deprecated
vtxOutWPOS BaseWPOSVS(vtxInWPOS IN)
{
  vtxOutWPOS OUT = (vtxOutWPOS)0; 
  OUT.HPosition = Get2dHPos(IN.Position);
  OUT.baseTC.xy = IN.baseTC.xy;
  OUT.CamVec.xyz = IN.CamVec;
  return OUT;
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Color grading/charts tex lookup
void TexColorChart2D(in sampler2D chartSampler, inout half3 cImage)
{
	const float chartDim	= 16.0;
	const float3 scale = float3(chartDim - 1.0, chartDim - 1.0, chartDim - 1.0) / chartDim;
	const float3 bias = float3(0.5, 0.5, 0.0) / chartDim;

	//cImage = float3(0.5, 1.0, 4.0 / chartDim); // debug lookup color to test addressing of "unwrapped" chart slices
	
	float3 lookup = saturate(cImage) * scale + bias;
	
	// convert input color into 2d color chart lookup address 
	float slice = lookup.z * chartDim;	
	float sliceFrac = frac(slice);	
	float sliceIdx = slice - sliceFrac;
	
	lookup.x = (lookup.x + sliceIdx) / chartDim;
	
 	// lookup adjacent slices
 	float3 col0 = GetTexture2D(chartSampler, lookup.xy); 	
 	lookup.x += 1.0 / chartDim;
 	float3 col1 = GetTexture2D(chartSampler, lookup.xy);

	// linearly blend between slices 	 	
	cImage = col0 + (col1 - col0) * sliceFrac;
 	//cImage = sliceFrac;
}

// Color grading/charts tex lookup
void TexColorChart2D(in Texture2D chartTex, in SamplerState chartSampler, inout half3 cImage)
{
	const float chartDim	= 16.0;
	const float3 scale = float3(chartDim - 1.0, chartDim - 1.0, chartDim - 1.0) / chartDim;
	const float3 bias = float3(0.5, 0.5, 0.0) / chartDim;

	//cImage = float3(0.5, 1.0, 4.0 / chartDim); // debug lookup color to test addressing of "unwrapped" chart slices
	
	float3 lookup = saturate(cImage) * scale + bias;
	
	// convert input color into 2d color chart lookup address 
	float slice = lookup.z * chartDim;	
	float sliceFrac = frac(slice);	
	float sliceIdx = slice - sliceFrac;
	
	lookup.x = (lookup.x + sliceIdx) / chartDim;
	
 	// lookup adjacent slices
 	float3 col0 = GetTexture2D(chartTex, chartSampler, lookup.xy); 	
 	lookup.x += 1.0 / chartDim;
 	float3 col1 = GetTexture2D(chartTex, chartSampler, lookup.xy);

	// linearly blend between slices 	 	
	cImage = col0 + (col1 - col0) * sliceFrac;
 	//cImage = sliceFrac;
}
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

Texture2DMS<float4> _tex0MS : register(t0);
Texture2DMS<float4> _tex1MS : register(t1);
Texture2DMS<float4> _tex2MS : register(t2);
Texture2DMS<float4> _tex3MS : register(t3);
Texture2DMS<float4> _tex4MS : register(t4);
Texture2DMS<float4> _tex5MS : register(t5);
Texture2DMS<float4> _tex6MS : register(t6);
Texture2DMS<float4> _tex7MS : register(t7);

half4 GetTargetIndexMS( int nIndex, float2 baseTC, int NumSamples, int nCurrSample ) 
{
	int3 vPixCoord = int3( baseTC * PS_ScreenSize.xy, 0);
#if %_RT_SAMPLE0 || %_RT_MSAA_QUALITY || %_RT_MSAA_QUALITY1
	if( nIndex == 0 )
		return _tex0MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 1 )
	  return _tex1MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 2 )
		return _tex2MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 3 )
		return _tex3MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 4 )
		return _tex4MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 5 )
	  return _tex5MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 6 )
		return _tex6MS.Load(vPixCoord, nCurrSample);
	if( nIndex == 7 )
		return _tex7MS.Load(vPixCoord, nCurrSample);
#endif
	return half4(0,0,0,0);
}

/////////////////////// eof ///
